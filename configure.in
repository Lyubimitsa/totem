AC_PREREQ(2.52)
AC_INIT(totem, 0.99.2, http://bugzilla.gnome.org/enter_bug.cgi?product=totem)
AC_CONFIG_SRCDIR(src/totem.c)
AM_INIT_AUTOMAKE(AC_PACKAGE_NAME, AC_PACKAGE_VERSION)
AM_CONFIG_HEADER(config.h)

AC_DEFINE(PACKAGE, AC_PACKAGE_NAME, [package name])
AC_DEFINE(VERSION, AC_PACKAGE_VERSION, [package version])
AC_DEFINE(GETTEXT_PACKAGE, "AC_PACKAGE_NAME", [foo])
GETTEXT_PACKAGE=AC_PACKAGE_NAME
AC_SUBST(GETTEXT_PACKAGE)

AC_PROG_CXX
AC_PROG_LIBTOOL()
AC_PROG_INTLTOOL([0.20])

GNOME_COMPILE_WARNINGS

dnl FreeBSD support
AC_CHECK_LIB(cam, cam_open_spec_device)

dnl xine or gst ?

GST_REQS=0.6.0.3
XINE_REQS=1.0.0

AC_ARG_ENABLE(gstreamer,
		AC_HELP_STRING([--enable-gstreamer],[use GStreamer instead of xine for the backend]),
	[case "${enableval}" in
	yes) ENABLE_GST=yes ;;
        no)  ENABLE_GST=no ;;
	*) AC_MSG_ERROR(bad value ${enableval} for --enable-gstreamer) ;;
	esac],
	[ENABLE_GST=no]) dnl Default value

AC_MSG_CHECKING(for backend libraries)

if test x$ENABLE_GST = xyes; then

dnl Now we're ready to ask for gstreamer libs and cflags
dnl And we can also ask for the right version of gstreamer
	HAVE_GSTREAMER=no

dnl start with 0.7
	GST_MAJORMINOR=0.7
	PKG_CHECK_MODULES(GST, \
			gstreamer-play-$GST_MAJORMINOR >= $GST_REQS \
			gstreamer-gconf-$GST_MAJORMINOR >= $GST_REQS,
			HAVE_GSTREAMER=yes,HAVE_GSTREAMER=no)

dnl try 0.6
	if test "x$HAVE_GSTREAMER" = "xno"; then
		GST_MAJORMINOR=0.6
		PKG_CHECK_MODULES(GST, \
			gstreamer-play-$GST_MAJORMINOR >= $GST_REQS \
			gstreamer-gconf-$GST_MAJORMINOR >= $GST_REQS,
			HAVE_GSTREAMER=yes,HAVE_GSTREAMER=no)
	fi

dnl Give error and exit if we don't have gstreamer
	if test "x$HAVE_GSTREAMER" = "xno"; then
		AC_MSG_ERROR(you need gstreamer development packages installed !)
	fi

	MM="gstreamer-libs-$GST_MAJORMINOR >= $GST_REQS gstreamer-play-$GST_MAJORMINOR >= $GST_REQS gstreamer-gconf-$GST_MAJORMINOR >= $GST_REQS"

	AC_MSG_RESULT(GStreamer)
else
	MM="libxine >= $XINE_REQS gconf-2.0"
	AC_MSG_RESULT(xine)
fi
AM_CONDITIONAL(TOTEM_GST, test x$ENABLE_GST = "xyes")

dnl CURL tests
CURL_MAJOR_REQ=7
CURL_MINOR_REQ=9
CURL_MICRO_REQ=8
AC_PATH_PROG(CURL_CONFIG, curl-config, no)
AC_MSG_CHECKING(for curl)
if test "$CURL_CONFIG" = "no" ; then
  AC_MSG_WARN(curl development libraries missing, no automatic DLL downloader)
else
  curl_major=`curl-config --version | sed 's/libcurl\ \(@<:@0-9@:>@*\).\(@<:@0-9@:>@*\).\(@<:@0-9@:>@*\)/\1/'`
  curl_minor=`curl-config --version | sed 's/libcurl\ \(@<:@0-9@:>@*\).\(@<:@0-9@:>@*\).\(@<:@0-9@:>@*\)/\2/'`
  curl_micro=`curl-config --version | sed 's/libcurl\ \(@<:@0-9@:>@*\).\(@<:@0-9@:>@*\).\(@<:@0-9@:>@*\)/\3/'`
  if `test $curl_major -gt $CURL_MAJOR_REQ || (test $curl_major -eq $CURL_MAJOR_REQ && test $curl_minor -gt $CURL_MINOR_REQ)|| (test $curl_major -eq $CURL_MAJOR_REQ && test $curl_minor -eq $CURL_MINOR_REQ && test $curl_micro -ge $CURL_MICRO_REQ)` ; then
	AC_MSG_RESULT(found $curl_major.$curl_minor.$curl_micro)
	CURL_CFLAGS=`$CURL_CONFIG --cflags`
	CURL_LIBS=`$CURL_CONFIG --libs`
	AC_SUBST(CURL_CFLAGS)
	AC_SUBST(CURL_LIBS)
  else
        AC_MSG_WARN(curl development libraries not new enough ( needed >= $CURL_MAJOR_REQ.$CURL_MINOR_REQ.$CURL_MICRO_REQ, got $curl_major.$curl_minor.$curl_micro, no automatic DLL downloader))
        CURL_CONFIG="no"
  fi
fi


dnl Seems that the order matters because libtool blows
PKG_CHECK_MODULES(EXTRA_GNOME, glib-2.0 >= 2.1.0 libgnomeui-2.0 >= 2.1.1 libglade-2.0 gnome-vfs-2.0 >= 2.1.6 gnome-desktop-2.0 >= 2.1.5 $MM)
EXTRA_GNOME_CFLAGS="$EXTRA_GNOME_CFLAGS"
EXTRA_GNOME_LIBS="$EXTRA_GNOME_LIBS"
AC_SUBST(EXTRA_GNOME_CFLAGS)
AC_SUBST(EXTRA_GNOME_LIBS)

PKG_CHECK_MODULES(GTK, gtk+-2.0 $MM gthread-2.0)
AC_SUBST(GTK_CFLAGS)
AC_SUBST(GTK_LIBS)


dnl LIRC

AC_ARG_ENABLE(lirc,
	AC_HELP_STRING([--enable-lirc],[enable lirc support]),
	[case "${enableval}" in
	yes) ENABLE_LIRC=yes ;;
	no)  ENABLE_LIRC=no ;;
	*) AC_MSG_ERROR(bad value ${enableval} for --enable-lirc) ;;
	esac],
        [ENABLE_LIRC=yes]) dnl Default value

if test x$ENABLE_LIRC = xyes ; then
AC_CHECK_HEADER(irman.h,,)
REMOTE_LIBS=""
AC_CHECK_LIB(lirc_client, lirc_init, [
	AC_CHECK_HEADER(lirc/lirc_client.h, [
	REMOTE_LIBS="-llirc_client"
	AC_DEFINE(HAVE_REMOTE, 1, [defined if you have lirc library])
	])
])
fi

AC_SUBST(REMOTE_LIBS)

AC_PATH_X

CFLAGS="-I$x_includes $CFLAGS"
LIBS="-L$x_libraries $LIBS"

XTEST_LIBS=""
AC_CHECK_LIB(Xtst, XTestFakeKeyEvent,
		XTEST_LIBS="-lXtst -L$x_libraries"
		AC_DEFINE(HAVE_XTEST, 1, [defined if you have XTest library]),
		,
		"-L$x_libraries")
AC_SUBST(XTEST_LIBS)

have_randr=no
AC_CHECK_LIB(Xrandr, XRRUpdateConfiguration,
	[AC_CHECK_HEADER(X11/extensions/Xrandr.h,
		have_randr=yes
		RANDR_LIBS="-lXrandr -lXrender"
		AC_DEFINE(HAVE_RANDR, 1, Have the Xrandr extension library),
		:, [#include <X11/Xlib.h>])], : ,
	-lXrandr -lXrender $x_libs)
AM_CONDITIONAL(HAVE_RANDR, [test $have_randr = yes])
AC_SUBST(RANDR_LIBS)

dnl Multimedia keys
have_xfree=no
AC_TRY_COMPILE([
	#include <X11/XF86keysym.h>
	],[
	int arg = 0;
	],[
	have_xfree=yes
])
AC_MSG_CHECKING(for X11 XFree86 headers)
AC_MSG_RESULT($have_xfree)
if test x"$have_xfree" = "xyes" ; then
	AC_DEFINE(HAVE_XFREE, 1, [defined if you have X11/XF86keysym.h])
fi

have_xsun=no
AC_TRY_COMPILE([
	#include <X11/Sunkeysym.h>
	],[
	int arg = 0;
	],[
	have_xsun=yes
])
AC_MSG_CHECKING(for X11 Sun headers)
AC_MSG_RESULT($have_xsun)
if test x"$have_xsun" = "xyes" ; then
	AC_DEFINE(HAVE_XSUN, 1, [defined if you have X11/Sunkeysym.h])
fi

if test x"$have_xsun" = "xno" && test x"$have_xfree" = "xno" ; then
	AC_MSG_ERROR(Your system doesn't have any headers for multimedia buttons)
fi

AC_MSG_CHECKING(for broken old architecture)
if test "`uname -m`x" = "i686x" ; then
       AC_MSG_RESULT(h0rk3d)
else
       AC_MSG_RESULT(n334)
fi
AC_MSG_CHECKING(for broken user)
if test "`whoami`x" = "hadessx" ; then
       AC_MSG_RESULT(h0rk3d)
else
       AC_MSG_RESULT(n334)
fi

AC_MSG_CHECKING(for Linux)
case `uname` in
	*Linux*)
	AC_MSG_RESULT(yes)
	IS_LINUX="yes"
	;;
	*)
	AC_MSG_RESULT(no)
	IS_LINUX="no"
	;;
esac
AM_CONDITIONAL(HAVE_LINUX, `test x$IS_LINUX = xyes`)

_system_is_x86="no"
AC_CHECK_HEADER( asm/vm86.h, _system_is_x86="yes" )
if `test "x$_system_is_x86" = "xyes" && test "x$CURL_CONFIG" != "xno"` ; then
	HAVE_X86=1
	AC_DEFINE(HAVE_X86, 1, [defined on x86 machines])
else
	HAVE_X86=0
fi
AM_CONDITIONAL(HAVE_X86, `test "x$_system_is_x86" = "xyes" && test "x$CURL_CONFIG" != "xno"`)

AC_PATH_PROG(GCONFTOOL, gconftool-2)
AM_GCONF_SOURCE_2

dnl debug
AC_ARG_ENABLE(debug,
		AC_HELP_STRING([--disable-debug],[disables compilation of debugging messages]),
	[case "${enableval}" in
	yes) ENABLE_DEBUG=yes ;;
	no)  ENABLE_DEBUG=no ;;
	*) AC_MSG_ERROR(bad value ${enableval} for --disable-debug) ;;
	esac],
[ENABLE_DEBUG=no]) dnl Default value
if test x$ENABLE_DEBUG = xyes; then
	AC_DEFINE(TOTEM_DEBUG, 1, [Define if DEBUG statements should be compiled in])
fi

dnl Add the languages which your application supports here.
ALL_LINGUAS="am bg be ca cs cy da de en_GB es et fi fr ga he it ja ko lv ms nl no pl pt pt_BR ru sr sr@Latn sv tr uk vi zh_CN zh_TW"
AM_GLIB_GNU_GETTEXT

AC_OUTPUT([
Makefile
totem.spec
src/Makefile
data/Makefile
po/Makefile.in
])

echo
if test x$ENABLE_GST = xno ; then
	echo "Using the xine backend"
else
	echo "Using the GStreamer backend"
fi
echo
